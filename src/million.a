;license:MIT
;(c) 2020 by 4am
;
!cpu 6502
!to "build/MILLION.SYSTEM",plain
*=$2000

column = $FC
row = $FD

         !source "src/constants.a"              ; no code
         !source "src/macros.a"                 ; no code
         !source "src/million.init.a"           ; one-time initialization code, exits via Start
         !source "src/hw.vbl.init.a"            ; TODO hook this up

FirstMover
!pseudopc $4000 {
Start
         jsr   LoadProgressFromDisk
         jsr   TitlePage
         ; C set if main menu requires full redraw (e.g. title sequence ended prematurely)
@GoToMainMenu
         jsr   MainMenu
         bcc   +
         jmp   Quit
+        sta   gWorldID
         jsr   MaybeLoadWorldFromDisk           ; A = world ID from selection page
         bcc   @PlayNext
         jmp   $FF59 ; TODO
@PlayNext
         jsr   LoadProgressFromMemory
                                                ; A = next puzzle ID
@Play
         sta   gPuzzleID
         ldx   gWorldID
         lda   kWorldLeftMargins, x
         sta   GlobalLeftMargin
         lda   kPuzzleWidths, x
         jsr   InitPuzzleStorage
         ldx   gPuzzleID
         jsr   LoadPuzzleFromMemory
         lda   #0
         sta   gSelectedLogicalColumn
         jsr   DrawPuzzleChrome
         jsr   ClearAndDrawPuzzle
         jsr   AnimatePuzzleIntoPlace
         jsr   DrawColumnSelectionIndicator
         jsr   PlayEventLoop
         cmp   #kCompletedPuzzle
         bne   +
         jsr   AnimatePuzzleCompleted
         ldx   gPuzzleID
         jsr   MarkPuzzleCompleted
                                                ; A - next puzzle ID
         jmp   @Play
+        cmp   #kRequestedRestart
         bne   +
         jsr   Home
         lda   gPuzzleID
         jmp   @Play
+
         sec
         bcs   @GoToMainMenu

         !source "src/puzzle.a"
         !source "src/hw.vbl.a"
         !source "src/hw.storage.a"
         !source "src/glue.mli.a"
         !source "src/ui.title.a"
         !source "src/ui.main.menu.a"
         !source "src/ui.play.a"
         !source "src/ui.common.a"
         !source "src/ui.font.courier.double.prime.a"
         !source "src/ui.font.courier.double.prime.data.a"
         !source "src/ui.font.heavy.silk.a"
         !source "src/ui.font.heavy.silk.data.a"
}
