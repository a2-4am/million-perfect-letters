; ELECTRIC DUET MUSIC PLAYER FOR THE MOCKINGBOARD
; COPYRIGHT 2014 CYBERNESTO
;
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the
; Free Software Foundation, Inc.,
; 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.

!zone {

CHN       = $1D
SONG      = $1E
LEFTCHN   = $00
RIGHTCHN  = $02
ENAREG    = $07
VOL_A     = $08
VOL_B     = $09
TONE      = $06
DURATION  = $08


!align $FF,0,0           ; align to page
MockingDuet
          JSR INIT
          JSR RESET
          JSR ENACHN
          JMP LOOP

SETVOL
NEXT      LDA SONG
          CLC
          ADC #$03
          STA SONG
          BCC LOOP
          INC SONG+1
LOOP      LDY #$00
          LDA (SONG),Y
          CMP #$01
          BEQ SETVOL
          BPL SETNOTE    ;SET DURATION
END       JSR RESET
          RTS

SETNOTE   STA DURATION
          LDA #LEFTCHN
SEND      STA CHN
MBSLOT1=*+2
          STA $C401
          JSR SETREG1
          INY
          LDA (SONG),Y
          BEQ SKIP       ;IF 0 KEEP LTTSA
          JSR CONVFREQ
SKIP      LDA TONE
MBSLOT2=*+2
          STA $C401
          JSR WRDATA1
          INC CHN
          LDA CHN
MBSLOT3=*+2
          STA $C401
          JSR SETREG1
          LDA TONE+1
MBSLOT4=*+2
          STA $C401
          JSR WRDATA1
          LDA #RIGHTCHN
          STA CHN
          CPY #$02
          BNE SEND
          LDX DURATION
W1        LDY TEMPO
W2        DEC TEMP
          BNE W2
          DEY
          BNE W2
          DEX
          BNE W1
          BIT $C000
          BMI END
          JMP NEXT

CONVFREQ  LDX OCTAVE
          INX
          PHA
          LDA #$00
          STA TONE+1
          PLA
DECOCT    DEX
          BMI LOBYTE
          ASL
          ROL TONE+1
          JMP DECOCT
LOBYTE    STA TONE
          RTS


RESET     LDA #$00
MBSLOT5=*+2
          STA $C400
MBSLOT6=*+2
          STA $C480
          LDA #$04
MBSLOT7=*+2
          STA $C400
MBSLOT8=*+2
          STA $C480
          RTS

INIT      LDA #$FF
MBSLOT9=*+2
          STA $C403
MBSLOT10=*+2
          STA $C483
          LDA #$07
MBSLOT11=*+2
          STA $C402
MBSLOT12=*+2
          STA $C482
          RTS

SETREG1   LDA #$07
MBSLOT13=*+2
          STA $C400
          LDA #$04
MBSLOT14=*+2
          STA $C400
          RTS

WRDATA1   LDA #$06
MBSLOT15=*+2
          STA $C400
          LDA #$04
MBSLOT16=*+2
          STA $C400
          RTS

ENACHN    LDA #ENAREG
MBSLOT17=*+2
          STA $C401
          JSR SETREG1
          LDA #%00111100
MBSLOT18=*+2
          STA $C401
          JSR WRDATA1
          LDA #VOL_A
MBSLOT19=*+2
          STA $C401
          JSR SETREG1
          LDA #$0F
MBSLOT20=*+2
          STA $C401
          JSR WRDATA1
          LDA #VOL_B
MBSLOT21=*+2
          STA $C401
          JSR SETREG1
          LDA #$0F
MBSLOT22=*+2
          STA $C401
          JSR WRDATA1
          RTS

OCTAVE    !byte 1
TEMPO     !byte 8
TEMP      !byte 0
}
