;license:MIT
;(c) 2020 by 4am
;
; common graphics functions
;

GlobalLeftMargin
         ; TODO other values for smaller puzzles
         ; $0A for 7-letter puzzles
         !byte $0A

Home
; in:    none, but assumes text screen 1 is clear (this is done at program startup)
; out:   hi-res page 1 cleared and displayed
         bit   PAGE1
         bit   FULLGFX
         bit   HIRES
         bit   TEXTMODE
         ldx   #$20
         stx   @erase+2
         ldy   #$00
         tya
@erase   sta   $2000,y
         iny
         bne   @erase
         inc   @erase+2
         dex
         bne   @erase
         bit   GFXMODE
         rts

WaitForKeyWithTimeout
; in:    A = timeout length (like standard $FCA8 wait routine)
; out:   A clobbered
;        X/Y preserved
         sec
@wait1   pha
@wait2   sbc   #1
         bne   @wait2
         pla
         bit   $C000
         bmi   @exit
         sbc   #1
         bne   @wait1
@exit    rts

ScrollDown
; in:    Y = logical column to scroll
; out:   X/Y preserved
         stx   @x+1
         sty   @y+1
         ldx   #$16
         jsr   LogicalColumnToPhysicalColumn
         jsr   InitScrollStorage
         jsr   WaitForVBL
         jsr   GENSCROLLDOWN
         jsr   UnwaitForVBL
@x       ldx   #$FD                  ; SMC
@y       ldy   #$FD                  ; SMC
         rts

ScrollUp
; in:    Y = logical column to scroll
; out:   X/Y preserved
         stx   @x+1
         sty   @y+1
         ldx   #$16
         jsr   LogicalColumnToPhysicalColumn
         jsr   InitScrollStorage
         jsr   WaitForVBL
         jsr   GENSCROLLUP
         jsr   UnwaitForVBL
@x       ldx   #$FD                  ; SMC
@y       ldy   #$FD                  ; SMC
         rts

InitScrollStorage
         lda   #$00
         sta   $FC
         sta   $FD
         sta   $FE
         sta   $FF
         rts

LogicalColumnToPhysicalColumn
; in:    Y contains logical column number
; out:   Y contains physical byte offset to use against an HGR base address
;        A clobbered
;        X preserved
         lda   GlobalLeftMargin
         clc
         bcc   +
-        adc   #$03
+        dey
         bpl   -
         tay
         rts

;------------------------------------------------------------------------------
; SoftBell
;
; in:    none
; out:   all registers and flags clobbered
;------------------------------------------------------------------------------
SoftBell
         ldx   #32
-        lda   #2
         jsr   ROM_WAIT
         bit   SPEAKER
         lda   #33
         jsr   ROM_WAIT
         bit   SPEAKER
         dex
         bne   -
         rts

nonZeroDigits = $EE
paddingCharacter = $EF
ToASCIIString
; convert byte value to length-prefixed 3-digit decimal number as ASCII string with given padding character
; in:    X = any number (0..255 obviously)
;        A = padding character (e.g. '0' or ' ')
; out:   $F1 = 0x03
;        $F2..$F4 = ASCII digits of decimal representation
;        clobbers $EE,$EF,$F0
;        all flags & registers clobbered
         sta   paddingCharacter
         stx   $F0
         ldx   #0
         stx   nonZeroDigits
         stx   $F1
@outer   lda   #0
         pha
@inner   lda   $F0
         cmp   @kPowersOfTen, x
         bcc   @digitDone
         sbc   @kPowersOfTen, x
         sta   $F0
         lda   $F1
         sbc   #0
         sta   $F1
         pla
         adc   #0
         pha
         jmp   @inner
@digitDone
         pla
         beq   @maybeUsePaddingChar
         inc   nonZeroDigits
-        ora   #$30
         bne   +                     ; always branches
@maybeUsePaddingChar
         cpx   #2
         beq   -
         ldy   nonZeroDigits
         bne   -
         lda   paddingCharacter
+        sta   $F2, x
         inx
         cpx   #$03
         bcc   @outer
         lda   #3                    ; length byte
         sta   $F1
         rts
@kPowersOfTen
         !byte 100
         !byte 10
         !byte 1
