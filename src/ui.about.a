;license:MIT
;(c) 2020 by 4am
;
; animated about screen
;
; Public functions:
; - AboutPage
;

revealindex = $F5

revealStringsLo
         !byte <kAboutWritten
         !byte <kAboutTested
         !byte <kAboutTested2
         !byte <kAboutMusic
         !byte <kAboutElectricDuet
         !byte <kAboutMockingDuet
         !byte 0
revealStringsHi
         !byte >kAboutWritten
         !byte >kAboutTested
         !byte >kAboutTested2
         !byte >kAboutMusic
         !byte >kAboutElectricDuet
         !byte >kAboutMockingDuet
         !byte 0

revealVTABs
         !byte 4
         !byte 20
         !byte 21
         !byte 9
         !byte 14
         !byte 19
revealHTABs
         !byte 5
         !byte 16
         !byte 20
         !byte 17
         !byte 9
         !byte 1
kAboutWritten
         !byte 14
         !raw "WRITTEN BY 4AM"
kAboutTested
         !byte 21
         !raw "PLAYTESTED BY OPTION8"
kAboutTested2
         !byte 10
         !raw "AND BESLEY"
kAboutMusic
         !byte 22
         !raw "MUSIC BY GABRIEL FAURE"
kAboutElectricDuet
         !byte 28
         !raw "SOUND ROUTINES BY PAUL LUTUS"
kAboutMockingDuet
         !byte 35
         !raw "MOCKINGBOARD ROUTINES BY CYBERNESTO"

AboutPage
; in:    none
; out:   everything clobbered
         jsr   Home
@outerloop
         lda   #0
         sta   revealindex
@loop    ldx   revealindex
         lda   revealVTABs, x
         sta   targetVTAB
         lda   revealHTABs, x
         sta   minHTAB
         lda   revealStringsHi, x
         beq   @outerloop
         tay
         lda   revealStringsLo, x
         jsr   HeavySilkReveal
         ldx   #10
         lda   #0
-        jsr   WaitForKeyWithTimeout
         bmi   @exit
         dex
         bpl   -
         inc   revealindex
         bne   @loop
@exit    rts
