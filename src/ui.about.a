;license:MIT
;(c) 2020 by 4am
;
; animated about screen
;
; Public functions:
; - AboutPage
;

aboutStringsLo
         !byte <sAboutWritten
         !byte <sAboutTested
         !byte <sAboutTested2
         !byte <sAboutMusic
         !byte <sAboutElectricDuet
         !byte <sAboutMockingDuet
         !byte <sAboutInspired
         !byte 0
aboutStringsHi
         !byte >sAboutWritten
         !byte >sAboutTested
         !byte >sAboutTested2
         !byte >sAboutMusic
         !byte >sAboutElectricDuet
         !byte >sAboutMockingDuet
         !byte >sAboutInspired
         !byte 0

aboutVTABs
         !byte 4
         !byte 20
         !byte 21
         !byte 9
         !byte 14
         !byte 19
         !byte 11
aboutHTABs
         !byte 5
         !byte 16
         !byte 20
         !byte 17
         !byte 9
         !byte 1
         !byte 9

;------------------------------------------------------------------------------
; AboutPage
; display animated credits page
;
; in:    none
; out:   all registers & flags clobbered
;------------------------------------------------------------------------------
AboutPage
         jsr   Home
@outerloop
         lda   #0
         sta   @loop+1
@loop    ldx   #$FD                  ; SMC
         lda   aboutVTABs, x
         sta   targetVTAB
         lda   aboutHTABs, x
         sta   minHTAB
         lda   aboutStringsHi, x
         beq   @outerloop
         tay
         lda   aboutStringsLo, x
         jsr   HeavySilkReveal
         inc   @loop+1
         ldx   #11
         jsr   LongWaitForKeyWithTimeout
         bpl   @loop
@exit    rts
